// groovylint-disable NestedBlockDepth

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'com.diffplug.spotless' version '8.1.0'
    id 'io.gitee.pkmer.sonatype-central-publisher' version '1.1.0'
}

group = 'io.github.bzkf'
version = '0.1.7' // x-release-please-version
base.archivesName = 'obds2-to-obds3'

configurations {
    xsd2java
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

ext {
    jaxbTargetDir = file('src/generated/java')
}

sourceSets {
    main {
        java {
            srcDirs jaxbTargetDir
        }
    }
}

dependencies {
    api platform('com.fasterxml.jackson:jackson-bom:2.20.1')
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    api 'com.fasterxml.jackson.module:jackson-module-jakarta-xmlbind-annotations'
    api 'jakarta.validation:jakarta.validation-api:3.1.1'
    api 'commons-codec:commons-codec:1.20.0'
    api 'org.slf4j:slf4j-api:2.0.17'

    // JAXB code generation
    xsd2java 'com.sun.xml.bind:jaxb-xjc:4.0.6'
    xsd2java 'com.sun.xml.bind:jaxb-impl:4.0.6'

    testImplementation platform('org.junit:junit-bom:5.14.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

tasks.register('xsd2java') {

    doLast {
        jaxbTargetDir.mkdirs()

        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xsd2java.asPath)
        ant.jaxbTargetDir = jaxbTargetDir


        ant.xjc(
                destdir: "${jaxbTargetDir}",
                package: 'de.basisdatensatz.obds.v2',
                schema: 'src/main/resources/schema/ADT_GEKID_v2.2.3.xsd'
        )

        ant.xjc(
                destdir: "${jaxbTargetDir}",
                package: 'de.basisdatensatz.obds.v3',
                schema: 'src/main/resources/schema/oBDS_v3.0.4.xsd',
                binding: 'src/main/resources/schema/oBDS_v3.0.4.bindings.xjb'
        )

    }
}
compileJava.dependsOn xsd2java

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'obds2-to-obds3'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'oBDS v2 to oBDS v3'
                description = 'Library to map oBDS v2 into oBDS v3 messages'
                url = 'https://github.com/bzkf/obds2-to-obds3'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'BZKF'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/bzkf/obds2-to-obds3.git'
                    developerConnection = 'scm:git:ssh://github.com:bzkf/obds2-to-obds3.git'
                    url = 'https://github.com/bzkf/obds2-to-obds3'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'Local'
            url = layout.buildDirectory.dir('repos/bundles')
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    def signingKey = System.getenv('SIGNING_KEY')
    def signingPassword = System.getenv('SIGNING_KEY_PASSPHRASE')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

pkmerBoot {
    sonatypeMavenCentral {
        // the same with publishing.repositories.maven.url in the configuration.
        stagingRepository = layout.buildDirectory.dir('repos/bundles')
        /**
         * get username and password from
         * <a href='https://central.sonatype.com/account'> central sonatype account</a>
         */
        username = System.getenv('OSSRH_USERNAME')
        password = System.getenv('OSSRH_PASSWORD')
    }
}

// via <https://stackoverflow.com/questions/24936781/gradle-plugin-project-version-number>
task printVersion {
    doLast {
        println project.version
    }
}

spotless {
    java {
        target('**/*.java')
        targetExclude('src/generated/java/**/*.*')
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
